plugins {
    id 'org.springframework.boot' version '2.3.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id "org.openapi.generator" version "4.3.1"
}

group = 'com.kakao'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "${rootDir}/src/main/resources/swagger/api.yml".toString()
    outputDir = "${rootDir}/generated".toString()
    apiPackage = "com.kakao.moneythrowing.rest"
    modelPackage = "com.kakao.moneythrowing.rest.model"
    modelNameSuffix = "ApiModel"
    configOptions = [
            useTags: "true",
            delegatePattern: "true",
            dateLibrary: "java8",
            java8: "true",
            hideGenerationTimestamp: "true"
    ]
    validateSpec = true
}
compileJava.dependsOn("openApiGenerate")

sourceSets {
    main {
        java {
            srcDir file("${rootDir}/generated/src/main/java")
        }
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.1'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    runtimeOnly 'com.h2database:h2'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.code.gson:gson:2.8.5'
}

test {
    useJUnitPlatform()
}
